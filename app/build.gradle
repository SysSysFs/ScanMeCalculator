plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'kotlinx-serialization'
}

android {
    namespace 'com.example.scanmecalculator'
    compileSdk 33

    defaultConfig {
        applicationId "com.example.scanmecalculator"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    flavorDimensions "themeMode" , "imageSource"
    productFlavors {
        red {
            dimension "themeMode"
            applicationIdSuffix ".red"
        }
        green {
            dimension "themeMode"
            applicationIdSuffix ".green"
        }
        filesystem {
            dimension "imageSource"
            applicationIdSuffix ".filesystem"
        }
        camera {
            dimension "imageSource"
            applicationIdSuffix ".camera"
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled false
        }
    }
    applicationVariants.all{
        variant ->
            variant.outputs.each{
                output->

                    def newName = output.outputFile.name
                    if (variant.buildType.name == "debug"){
                        newName = newName.replace("-debug", "")
                    }
                    else if (variant.buildType.name == "release"){
                        newName = newName.replace("-release", "")
                        newName = newName.replace("-unsigned", "")
                        newName = newName.replace("-signed", "")
                    }

                    if(newName.contains("camera"))
                    {
                        newName = newName.replace("-camera", "-built-in-camera")
                    }

                    output.outputFileName = newName

            }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.0'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0'
    implementation 'androidx.activity:activity-compose:1.7.0'
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation "androidx.compose.material:material:$compose_ui_version"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.0'

    def nav_version = "2.5.3"
    implementation("androidx.navigation:navigation-compose:$nav_version")

    // timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    // coroutines
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4")

    // hilt
    implementation("com.google.dagger:hilt-android:2.44")
    kapt("com.google.dagger:hilt-android-compiler:2.44")
    implementation("androidx.hilt:hilt-navigation-compose:1.0.0")

    // CameraX core library using the camera2 implementation
    def camerax_version = "1.3.0-alpha05"
    implementation("androidx.camera:camera-core:${camerax_version}")
    implementation("androidx.camera:camera-camera2:${camerax_version}")
    implementation("androidx.camera:camera-lifecycle:${camerax_version}")
    implementation("androidx.camera:camera-view:${camerax_version}")
    implementation("androidx.camera:camera-mlkit-vision:${camerax_version}")
    implementation("androidx.camera:camera-extensions:${camerax_version}")

    // google mlkit text recognition
    implementation 'com.google.android.gms:play-services-mlkit-text-recognition:18.0.2'

    // coil
    implementation "io.coil-kt:coil-compose:1.3.2"

    // kotlin serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.0"

    // for file encryption
    implementation("androidx.security:security-crypto:1.0.0")

    // room db
    def room_version = "2.5.1"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

}

// Allow references to generated code
kapt {
    correctErrorTypes = true
}